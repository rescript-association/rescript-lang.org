---
title: "Object"
description: "Interoping with JS objects in ReScript"
canonical: "/docs/manual/latest/object"
---

# Object

ReScript objects are like [records](record.md), but:

- No type declaration needed.
- Structural and more polymorphic, [unlike records](record.md#record-types-are-found-by-field-name).
- Doesn't support updates unless the object comes from the JS side.
- Doesn't support [pattern matching](pattern-matching-destructuring.md).

<!-- TODO: support update man -->

Although ReScript records compile to clean JavaScript objects, ReScript objects are a better candidate for emulating/binding to JS objects, as you'll see.

## Type Declaration

**Optional**, unlike for records. The type of an object is inferred from the value, so you never really need to write down is type definition. Nevertheless, here's its type declaration syntax:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type person = {
  .
  "age": int,
  "name": string
};
```
```re
type person = <
  age :int;
  name :string> Js.t
```
```js
// Empty output
```

</CodeTab>

Visually similar to record type's syntax, with the field names quoted.

## Creation

To create a new object:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let me = {
  "age": 5,
  "name": "Big ReScript"
};
```
```ml
let me = [%bs.obj {
  age = 5;
  name = "Big ReScript";
}]
```
```js
var me = {
  "age": 5,
  "name": "Big ReScript"
};
```

</CodeTab>

**Note**: as said above, unlike for record, this `me` value does **not** try to find a conforming type declaration with the field `"age"` and `"name"`; rather, the type of `me` is inferred as `{"age": int, "name": string}`. This is convenient, but also means this code passes type checking without errors:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type person = {
  .
  "age": int
};

let me = {
  "age": "hello!" // age is a string. No error.
};
```
```ml
type person = <
  age :int
> Js.t

let me = [%bs.obj {
  age = "hello!" (* age is a string. No error. *)
}]
```
```js
var me = {
  "age": "hello!"
};
```

</CodeTab>

Since the type checker doesn't try to match `me` with the type `person`. If you ever want to force an object value to be of a predeclared object type, just annotate the value:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)"]}>

```re
let me: person = {
  "age": "hello!"
}
```
```ml
let me: person = [%bs.obj {
  age = "hello!"
}]
```

</CodeTab>

Now the type system will error properly.

## Access

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let age = me##age;
```
```ml
let age = me##age
```
```js
var age = me["age"];
```

</CodeTab>

## Update

Disallowed unless the object is a binding that comes from the JavaScript side. In that case, use `=`:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type student = {
  [@bs.set] "age": int,
  [@bs.set] "name": string,
};
[@bs.module "MyJSFile"] external student1: student = "student1";

student1##name = "Mary";
```
```ml
type student = <
  age: int [@bs.set];
  name: string [@bs.set];
> Js.t
external student1: student = "student1" [@@bs.module "MyJSFile"]

student1##name = "Mary"
```
```js
var MyJSFile = require("MyJSFile");
MyJSFile.student1.name = "Mary";
```

</CodeTab>

## Tips & Tricks

Since objects don't require type declarations, and since ReScript infers all the types for you, you get to very quickly and easily (and dangerously) bind to any JavaScript API. Check the JS output tab:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// The type of document is just some random type 'a
// that we won't bother to specify
[@bs.val] external document: 'a = "document";

// call a method
document##addEventListener("mouseup", _event => {
  Js.log("clicked!");
});

// get a property
let loc = document##location;

// set a property
document##location##href = "rescript-lang.org";
```
```ml
// The type of document is just some random type 'a
// that we won't bother to specify
external document: 'a = "document" [@@bs.val]

// call a method
let () = document##addEventListener "mouseup" (fun _event ->
  Js.log "clicked!"
)

// get a property
let loc = document##location

// set a property
document##location##href = "rescript-lang.org"
```
```js
document.addEventListener("mouseup", function(_event) {
  console.log("clicked!");
});
var loc = document.location;
document.location.href = "rescript-lang.org";
```

</CodeTab>

The `external` feature and the usage of this trick are also documented in the [external](external#tips--tricks) section later. It's an excellent way to start writing some ReScript code without worrying about whether bindings to a particular library exists.
