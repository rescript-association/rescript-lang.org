---
title: "Record"
description: "Record types in ReScript"
canonical: "/docs/manual/latest/record"
---

# Record

Records are like JavaScript objects but:

- are immutable by default
- have fixed fields (not extensible)

## Type Declaration

A record needs a mandatory type declaration:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type person = {
  age: int,
  name: string
};
```
```ml
type person = {
  age: int;
  name: string;
}
```
```js
// Empty output
```

</CodeTab>

## Creation

To create a `person` record (declared above):

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let me = {
  age: 5,
  name: "Big ReScript"
};
```
```ml
let me = {
  age = 5;
  name = "Big ReScript";
}
```
```js
var me = {
  age: 5,
  name: "Big ReScript"
};
```

</CodeTab>

When you create a new record value, ReScript tries to find a record type declaration that conforms to the shape of the value. So the `me` value here is inferred as of type `person`.

The type is found by looking above the `me` value. **Note**: if the type instead resides in another file or module, you need to explicitly indicate which file or module it is:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// School.re
type person = {age: int, name: string};
```
```ml
(* School.ml *)
type person = {age: int; name: string}
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// Example.re

let me: School.person = {age: 20, name: "Big ReScript"};
/* or */
let me2 = {School.age: 20, name: "Big ReScript"};
```
```ml
(* Example.re *)

let me: School.person = {age = 20; name = "Big ReScript"}
(* or *)
let me2 = {School.age = 20; name = "Big ReScript"}
```
```js
var me = {
  age: 20,
  name: "Big ReScript"
};
var me2 = {
  age: 20,
  name: "Big ReScript"
};
```

</CodeTab>

Either of the above 3 says "this record's definition is found in the School file". The first one, the regular type annotation, is preferred.

## Access

Use the familiar dot notation:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let name = me.name;
```
```ml
let name = me.name
```
```js
var name = "Big ReScript";
```

</CodeTab>

## Immutable Update

New records can be created from old records with the `...` spread operator. The original record isn't mutated.

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let meNextYear = {...me, age: me.age + 1};
```
```ml
let meNextYear = {me with age = me.age + 1}
```
```js
var meNextYear = {
  age: 21,
  name: "Big ReScript"
};
```

</CodeTab>

**Note**: spread cannot add new fields to the record value, as a record's shape is fixed by its type.

## Mutable Update

Record fields can optionally be mutable. This allows you to efficiently update those fields in-place with the `=` operator.

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type person = {
  name: string,
  mutable age: int
};

let baby = {name: "Baby ReScript", age: 5};
baby.age = baby.age + 1; // `baby.age` is now 6. Happy birthday!
```
```ml
type person = {
  name: string;
  mutable age: int;
}

let baby = {name = "Baby ReScript"; age = 5}
let () = baby.age <- baby.age + 1 (* `baby.age` is now 6. Happy birthday! *)
```
```js
var baby = {
  name: "Baby ReScript",
  age: 5
};

baby.age = baby.age + 1 | 0;
```

</CodeTab>

## Tips & Tricks

**Record Types Are Found By Field Name**. With records, you **cannot** say "I'd like this function to take any record type, as long as they have the field `age`". The following **won't work as intended**:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type person = {age: int, name: string};
type monster = {age: int, hasTentacles: bool};

let getAge = (entity) => entity.age;
```
```ml
type person = {age: int; name: string}
type monster = {age: int; hasTentacles: bool}

let getAge entity = entity.age
```
```js
function getAge(entity) {
  return entity.age;
}
```

</CodeTab>

Instead, `getAge` will infer that the parameter `entity` must be of type `monster`, the closest record type with the field `age`. The following code's last line fails:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)"]}>

```re
let kraken = {age: 9999, hasTentacles: true};
let me = {age: 5, name: "Baby ReScript"};

getAge(kraken);
getAge(me); // type error!
```
```ml
let kraken = {age = 9999; hasTentacles = true}
let me = {age = 5; name = "Baby ReScript"}

let () = getAge kraken
let () = getAge me (* type error! *)
```

</CodeTab>

The type system will complain that `me` is a `person`, and that `getAge` only works on `monster`. If you need such capability, use ReScript objects, described [here](object.md).

## Design Decisions

After reading the constraints in the previous sections, and if you're coming from a dynamic language background, you might be wondering why one would bother with record in the first place instead of straight using object, since the former needs explicit typing and doesn't allow different records with the same field name to be passed to the same function, etc.

1. The truth is that most of the times in your app, your data's shape is actually fixed, and if it's not, it can potentially be better represented as a combination of variant (introduced next) + record instead.
2. Since a record type is resolved through finding that single explicit type declaration (we call this "nominal typing"), the type error messages end up better than the counterpart ("structural typing", like for tuples). This makes refactoring easier; changing a record type's fields naturally allows the compiler to know that it's still the same record, just misused in some places. Otherwise, under structural typing, it might get hard to tell whether the definition site or the usage site is wrong.
