---
author: josh-derocher-vlk
date: "2025-01-01"
previewImg: /static/blog/compiler_release_11_1.jpg
title: What can I do with ReScript?
description: |
  Can I use Vite, or Next.js? Is it only for React? Can I use Node or Deno?
---

You've taken a look and ReScript and you want to try it out, but how do you get started? There's the [installation](https://rescript-lang.org/docs/manual/latest/installation) page in the docs, 
which is great if you want to set up a new React app using [create-rescript-app](https://github.com/rescript-lang/create-rescript-app). There's instructions on how to add it to an existing project or set it up manually.
But that doesn't really answer the question "Can I use this with X?".

## You can use ReScript anywhere you can use JavaScript
ReScript is just a language that compiles to JavaScript. Unlike other language like [Elm](https://elm-lang.org/) or [PureScript](https://www.purescript.org/) ReScript doesn't have a recommended framework or independent ecosystem, it's just part of the normal JavaScript world.

Here's a really basic example that you can run in Node after compiling:

```res
// index.res
Console.log("Hello")
```

Just run `node index.res.js` and you'll see "Hello" logged to the console. You can import compiled ReScript into any project that could import JavaScript.
But real world projects aren't just JavaScript; they use libraries and frameworks. This is where [bindings](https://rescript-lang.org/docs/manual/latest/external) come into play.
A binding is a way to tell ReScript the types and imports from external JavaScript. You can think of bindings in the same way that you need to create a `*.d.ts` file to add types to a JavaScript library that doesn't use TypeScript.

ReScript has great integration with [React](https://rescript-lang.org/docs/react/latest/introduction) and those bindings are kept up to date by the core team, but that doesn't mean you don't have other options!

## Using existing bindings
While ReScript isn't as large as TypeScript it has a small but growing list of bindings you can find on NPM. The website has a [package explorer](https://rescript-lang.org/packages) you can use to find official and community maintained bindings.
Many major libraries have existing bindings. Here's a small set of what you can find.

- [Node](https://github.com/TheSpyder/rescript-nodejs)
- [Material UI](https://github.com/cca-io/rescript-mui)
- [Bun](https://github.com/zth/rescript-bun)
- [Deno](https://github.com/tsirysndr/rescript-deno)
- [Deno's Fresh](https://github.com/jderochervlk/rescript-fresh)
- [Vitest](https://github.com/cometkim/rescript-vitest)
- [Rxjs](https://github.com/noble-ai/rescript-rxjs)
- [React Helmet](https://github.com/MoOx/rescript-react-helmet)
- [Jotai](https://github.com/Fattafatta/rescript-jotai)
- [Headless UI](https://github.com/cbowling/rescript-headlessui)


## Using libraries and frameworks created for ReScript
Bindings are great if you want to work with libraries written with JavaScript, but there are great options for libraries and frameworks written with ReScript, which means you don't need bindings.

- [ReScript Schema](https://github.com/DZakh/rescript-schema) - The fastest parser in the entire JavaScript ecosystem with a focus on small bundle size and top-notch DX.
- [rescript-relay](https://github.com/zth/rescript-relay) - This is an amazing way to connect React to Relay and GraphQL
- [rescript-rest](https://github.com/DZakh/rescript-rest) - Fully typed RPC-like client, with no need for code generation!
- [rescript-edgedb](https://github.com/zth/rescript-edgedb) - Use EdgeDB fully type safe in ReScript. Embed EdgeQL right in your ReScript source code.
- [ResX](https://github.com/zth/res-x) - A ReScript framework for building server-driven web sites and applications.

## Creating your own bindings
At some point you will probably have to use a library that doesn't have bindings available. Asking on the [forum](https://forum.rescript-lang.org/) is a great place to start. Someone else might have bindings already in a project that they just haven't published to NPM.
You can also get help and guidance on how to write bindings for what you need. Usually you can figure out what you need from looking at a libraries official docs. 
You don't need to write bindings for an entire library, or even for all of a functions arguments. Just write what you need as you go.

Let's take a look at the `format` function from [date-fns](https://date-fns.org/). We can see the [arguments in the docs](https://date-fns.org/v4.1.0/docs/format#arguments), and how it should be imported and used.
```res
// type signature
function format(
  date: string | number | Date,
  formatStr: string,
  options?: FormatOptions
): string

// how it's imported 
import { format } from "date-fns";

// how it's used
const result = format(new Date(2014, 1, 11), 'MM/dd/yyyy')
```

That's all we need to know to write bindings to use this function in ReScript. 
The first thing we need to figure out is how to handle the type for what `date-fns` considers to be a `date`, which is `Date | string | number`. In ReScript things can't just be of different types like they can in JavaScript or TypeScript. There are a couple options here; you can make a function for each type such as `formatString` and `formatDate`, or you can create a [variant type](https://rescript-lang.org/docs/manual/latest/variant) to map to the possible input types.
Creating a function for each type is simpler, and it's most likely how you will use the library in your project. You probably have a standard type for Dates already. We'll also need a type for `FormatDateOptions` in case we want to pass options. We'll use [labled arguments](https://rescript-lang.org/docs/manual/latest/function#labeled-arguments) for our binding.
```res
// DateFns.res - you might want to put this in a folder called "bindings" or "external"
type formatDateOptions // we're not even going to add anything to this yet until we need something

@module("date-fns") // this is the import path for the module
external formatString: (
  ~date: string, // the date string
  ~formatStr: string, // how we want it formatted
  ~options: formatDateOptions=?, // =? means the argument is optional
) => string = "format" // "format" is the name of the function we are importing from the module
```

Now we can use the function!
<CodeTab labels={["ReScript", "JS Output"]}>
```res
let formattedDate = DateFns.formatString(~date="2021-09-01", ~formatStr="MMMM dd, yyyy")
```

```js
import * as DateFns from "date-fns";

var formattedDate = DateFns.format("2021-09-01", "MMMM dd, yyyy");
```
</CodeTab>

If we need to use `FormatDateOptions` we can add to our type definition as needed. The first option is `firstWeekContainsDate` which can either be `1` or `4`.
Here's how we could write bindings for that.
```res
@unboxed
type firstWeekContainsDate =
  | @as(1) One
  | @as(4) Four

type formatDateOptions = {firstWeekContainsDate: firstWeekContainsDate}
```

And when we use it it will output either `1` or `4`.
<CodeTab labels={["ReScript", "JS Output"]}>
```res
let formattedDate = formatString(
  ~date="2021-09-01",
  ~formatStr="MMMM dd, yyyy",
  ~options={firstWeekContainsDate: Four},
)
```

```js
import * as DateFns from "date-fns";

var formattedDate = DateFns.format("2021-09-01", "MMMM dd, yyyy", {
      firstWeekContainsDate: 4
    });
```
</CodeTab>

You can write new bindings and extend existing types as you need.