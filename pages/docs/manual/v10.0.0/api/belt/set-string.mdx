# SetString

<Intro>

Specialized when value type is `string`, more efficient than the generic type, its compare behavior is fixed using the built-in comparison.

</Intro>

## value

```res prelude
type value = string
```

The type of the set elements.

## t

```res prelude
type t
```

The type of sets.

## empty

```res sig
let empty: t
```

Empty set

```res example
let s0 = Belt.Set.String.empty
```

## fromArray

```res sig
let fromArray: array<value> => t
```

Creates new set from array of elements.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "orange", "banana"])

s0->Belt.Set.String.toArray /* ["apple", "banana", "orange"] */
```

## fromSortedArrayUnsafe

```res sig
let fromSortedArrayUnsafe: array<value> => t
```

The same as [fromArray][#fromarray] except it is after assuming the input array is already sorted.

## isEmpty

```res sig
let isEmpty: t => bool
```

Checks if set is empty.

```res example
let empty = Belt.Set.String.fromArray([])
let notEmpty = Belt.Set.String.fromArray(["apple"])

Belt.Set.String.isEmpty(empty) /* true */
Belt.Set.String.isEmpty(notEmpty) /* false */
```

## has

```res sig
let has: (t, value) => bool
```

Checks if element exists in set.

```res example
let set = Belt.Set.String.fromArray(["apple", "orange", "banana"])

set->Belt.Set.String.has("strawberry") /* false */
set->Belt.Set.String.has("apple") /* true */
```

## add

```res sig
let add: (t, value) => t
```

Adds element to set. If element existed in set, value is unchanged.

```res example
let s0 = Belt.Set.String.empty
let s1 = s0->Belt.Set.String.add("apple")
let s2 = s1->Belt.Set.String.add("banana")
let s3 = s2->Belt.Set.String.add("banana")
s0->Belt.Set.String.toArray /* [] */
s1->Belt.Set.String.toArray /* ["apple"] */
s2->Belt.Set.String.toArray /* ["apple", "banana"] */
s3->Belt.Set.String.toArray /* ["apple", "banana"] */
s2 == s3 /* true */
```

## mergeMany

```res sig
let mergeMany: (t, array<value>) => t
```

Adds each element of array to set. Unlike [add](#add), the reference of return value might be changed even if all values in array already exist in set

```res example
let set = Belt.Set.String.empty

let newSet = set->Belt.Set.String.mergeMany(["apple", "banana", "orange", "strawberry"])

newSet->Belt.Set.String.toArray /* ["apple", "banana", "orange", "strawberry"] */
```

## remove

```res sig
let remove: (t, value) => t
```

Removes element from set. If element wasn't existed in set, value is unchanged.

```res example
let s0 = Belt.Set.String.fromArray(["orange", "banana", "apple"])
let s1 = s0->Belt.Set.String.remove("apple")
let s2 = s1->Belt.Set.String.remove("banana")
let s3 = s2->Belt.Set.String.remove("banana")

s1->Belt.Set.String.toArray /* ["orange", "banana"] */
s2->Belt.Set.String.toArray /* ["orange"] */
s2 == s3 /* true */
```

## removeMany

```res sig
let removeMany: (t, array<value>) => t
```

Removes each element of array from set. Unlike [remove](#remove), the reference of return value might be changed even if any values in array not existed in set.

```res example
let set = Belt.Set.String.fromArray(["apple", "banana", "orange"])

let newSet = set->Belt.Set.String.removeMany(["strawberry", "apple", "banana", "orange"])
newSet->Belt.Set.String.toArray /* [] */
```

## union

```res sig
let union: (t, t) => t
```

Returns union of two sets.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.Set.String.fromArray(["apple", "banana", "orange", "strawberry"])
let union = Belt.Set.String.union(s0, s1)
union->Belt.Set.String.toArray /* ["apple", "banana", "carrot", "orange", "strawberry"] */
```

## intersect

```res sig
let intersect: (t, t) => t
```

Returns intersection of two sets.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.Set.String.fromArray(["apple", "banana", "orange", "strawberry"])
let intersect = Belt.Set.String.intersect(s0, s1)
intersect->Belt.Set.String.toArray /* ["apple", "banana", "orange"] */
```

## diff

```res sig
let diff: (t, t) => t
```

Returns elements from first set, not existing in second set.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.Set.String.fromArray(["apple", "banana", "orange", "strawberry"])
Belt.Set.String.toArray(Belt.Set.String.diff(s0, s1)) /* ["carrot"] */
Belt.Set.String.toArray(Belt.Set.String.diff(s1, s0)) /* ["strawberry"] */
```

## subset

```res sig
let subset: (t, t) => bool
```

Checks if second set is subset of first set.

```res example
let s0 = Belt.Set.String.fromArray(["5", "2", "3", "5", "6"])
let s1 = Belt.Set.String.fromArray(["5", "2", "3", "1", "5", "4"])
let s2 = Belt.Set.String.intersect(s0, s1)
Belt.Set.String.subset(s2, s0) /* true */
Belt.Set.String.subset(s2, s1) /* true */
Belt.Set.String.subset(s1, s0) /* false */
```

## cmp

```res sig
let cmp: (t, t) => int
```

Total ordering between sets. Can be used as the ordering function for doing sets of sets. It compares size first and then iterates over each element following the order of elements.

## eq

```res sig
let eq: (t, t) => bool
```

Checks if two sets are equal.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "orange"])
let s1 = Belt.Set.String.fromArray(["orange", "apple"])

Belt.Set.String.eq(s0, s1) /* true */
```

## forEachU

```res sig
let forEachU: (t, (. value) => unit) => unit
```

Same as [forEach](##forEach) but takes uncurried functon.

## forEach

```res sig
let forEach: (t, value => unit) => unit
```

Applies function `f` in turn to all elements of set in increasing order.

```res example
let s0 = Belt.Set.String.fromArray(["banana", "orange", "apple"])
let acc = ref(list{})
s0->Belt.Set.String.forEach(x => acc := Belt.List.add(acc.contents, x))
acc /* ["orange", "banana", "apple"] */
```

## reduceU

```res sig
let reduceU: (t, 'a, (. 'a, value) => 'a) => 'a
```

## reduce

```res sig
let reduce: (t, 'a, ('a, value) => 'a) => 'a
```

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "orange"])
s0->Belt.Set.String.reduce(0, (acc, element) => acc + String.length(element)) /* 11 */
```

## everyU

```res sig
let everyU: (t, (. value) => bool) => bool
```

## every

```res sig
let every: (t, value => bool) => bool
```

Checks if all elements of the set satisfy the predicate. Order unspecified.

```res example
let hasAtLeastFiveChars = x => String.length(x) >= 5

let s0 = Belt.Set.String.fromArray(["apple", "carrot"])
s0->Belt.Set.String.every(hasAtLeastFiveChars) /* true */
```

## someU

```res sig
let someU: (t, (. value) => bool) => bool
```

## some

```res sig
let some: (t, value => bool) => bool
```

Checks if at least one element of the set satisfies the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.Set.String.fromArray(["strawberry", "apple"])
s0->Belt.Set.String.some(hasFiveChars) /* true */
```

## keepU

```res sig
let keepU: (t, (. value) => bool) => t
```

## keep

```res sig
let keep: (t, value => bool) => t
```

Returns the set of all elements that satisfy the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.Set.String.fromArray(["apple", "orange", "banana"])
let s1 = s0->Belt.Set.String.keep(hasFiveChars)

s1->Belt.Set.String.toArray /* ["apple"] */
```

## partitionU

```res sig
let partitionU: (t, (. value) => bool) => (t, t)
```

## partition

```res sig
let partition: (t, value => bool) => (t, t)
```

Returns a pair of sets, where first is the set of all the elements of set that satisfy the predicate, and second is the set of all the elements of set that do not satisfy the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.Set.String.fromArray(["apple", "carrot"])
let (s1, s2) = s0->Belt.Set.String.partition(hasFiveChars)

s1->Belt.Set.String.toArray /* ["apple"] */
s2->Belt.Set.String.toArray /* ["carrot"] */
```

## size

```res sig
let size: t => int
```

Returns size of the set.

```res example
let s0 = Belt.Set.String.fromArray(["apple"])

s0->Belt.Set.String.size /* 1 */
```

## toList

```res sig
let toList: t => list<value>
```

Returns list of ordered set elements.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "watermelon"])

s0->Belt.Set.String.toList /* ["apple", "watermelon"] */
```

## toArray

```res sig
let toArray: t => array<value>
```

Returns array of ordered set elements.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "watermelon"])

s0->Belt.Set.String.toArray /* ["apple", "watermelon"] */
```

## minimum

```res sig
let minimum: t => option<value>
```

Returns minimum value of the collection. `None` if collection is empty.

```res example
let s0 = Belt.Set.String.empty
let s1 = Belt.Set.String.fromArray(["apple", "orange"])

s0->Belt.Set.String.minimum /* None */
s1->Belt.Set.String.minimum /* Some("apple") */
```

## minUndefined

```res sig
let minUndefined: t => Js.undefined<value>
```

Returns minimum value of the collection. `undefined` if collection is empty.

```res example
let s0 = Belt.Set.String.empty
let s1 = Belt.Set.String.fromArray(["apple", "orange"])

s0->Belt.Set.String.minUndefined /* undefined */
s1->Belt.Set.String.minUndefined /* "apple" */
```

## maximum

```res sig
let maximum: t => option<value>
```

Returns maximum value of the collection. `None` if collection is empty.

```res example
let s0 = Belt.Set.String.empty
let s1 = Belt.Set.String.fromArray(["apple", "orange"])

s0->Belt.Set.String.maximum /* None */
s1->Belt.Set.String.maximum /* Some("orange") */
```

## maxUndefined

```res sig
let maxUndefined: t => Js.undefined<value>
```

Returns maximum value of the collection. `undefined` if collection is empty.

```res example
let s0 = Belt.Set.String.empty
let s1 = Belt.Set.String.fromArray(["apple", "orange"])

s0->Belt.Set.String.maxUndefined /* undefined */
s1->Belt.Set.String.maxUndefined /* orange */
```

## get

```res sig
let get: (t, value) => option<value>
```

Returns the reference of the value which is equivalent to value using the comparator specifiecd by this collection. Returns `None` if element does not exist.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "carrot"])

s0->Belt.Set.String.get("carrot") /* Some("carrot") */
s0->Belt.Set.String.get("watermelon") /* None */
```

## getUndefined

```res sig
let getUndefined: (t, value) => Js.undefined<value>
```

See [get](#get) - returns `undefined` when element does not exist.

## getExn

```res sig
let getExn: (t, value) => value
```

See [get](#get) - raise when element does not exist.

## split

```res sig
let split: (t, value) => ((t, t), bool)
```

Returns a triple `((l, r), present)`, where `l` is the set of elements of set that are strictly less than value, `r` is the set of elements of set that are strictly greater than value, `present` is `false` if set contains no element equal to value, or `true` if set contains an element equal to value.

```res example
let s0 = Belt.Set.String.fromArray(["apple", "banana", "orange"])

let ((smaller, larger), present) = s0->Belt.Set.String.split("banana")

present /* true */
smaller->Belt.Set.String.toArray /* ["apple"] */
larger->Belt.Set.String.toArray /* ["orange"] */
```

## checkInvariantInternal

```res sig
let checkInvariantInternal: t => unit
```

**raise** when invariant is not held

