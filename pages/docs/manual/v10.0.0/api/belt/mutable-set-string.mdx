# MutableSetString

<Intro>

This module is [Belt.MutableSet](mutable-set) specialized with key type to be a `string` type.
It is more efficient in general, the API is the same with [Belt.MutableSet](mutable-set) except its key type is fixed, and identity is not needed (using the built-in one)

</Intro>

## value

```res prelude
type value = string
```

The type of the set elements.

## t

```res prelude
type t
```

The type of sets.

## make

```res sig
let make: unit => t
```

Returns empty set.

```res example
let set = Belt.MutableSet.String.make()
```

## fromArray

```res sig
let fromArray: array<value> => t
```

Creates new set from array of elements.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "orange", "banana"])

s0->Belt.MutableSet.String.toArray /* ["apple", "banana", "orange"] */
```

## fromSortedArrayUnsafe

```res sig
let fromSortedArrayUnsafe: array<value> => t
```

The same as [fromArray][#fromarray] except it is after assuming the input array is already sorted.

## copy

```res sig
let copy: t => t
```

Returns copy of a set.

```res example
let s0 = Belt.MutableSet.String.fromArray(["orange", "apple"])

let copied = s0->Belt.MutableSet.String.copy
copied->Belt.MutableSet.String.toArray /* ["apple", "orange"] */
```

## isEmpty

```res sig
let isEmpty: t => bool
```

Checks if set is empty.

```res example
let empty = Belt.MutableSet.String.fromArray([])
let notEmpty = Belt.MutableSet.String.fromArray(["apple"])

Belt.MutableSet.String.isEmpty(empty) /* true */
Belt.MutableSet.String.isEmpty(notEmpty) /* false */
```

## has

```res sig
let has: (t, value) => bool
```

Checks if element exists in set.

```res example
let set = Belt.MutableSet.String.fromArray(["apple", "orange", "banana"])

set->Belt.MutableSet.String.has("strawberry") /* false */
set->Belt.MutableSet.String.has("apple") /* true */
```

## add

```res sig
let add: (t, value) => unit
```

Adds element to set. If element existed in set, value is unchanged.

```res example
let s0 = Belt.MutableSet.String.make()
s0->Belt.MutableSet.String.add("apple")
s0->Belt.MutableSet.String.add("banana")
s0->Belt.MutableSet.String.add("banana")

s0->Belt.MutableSet.String.toArray /* ["apple", "banana"] */
```

## addCheck

```res sig
let addCheck: (t, value) => bool
```

## mergeMany

```res sig
let mergeMany: (t, array<value>) => unit
```

Adds each element of array to set.

```res example
let set = Belt.MutableSet.String.make()

set->Belt.MutableSet.String.mergeMany(["apple", "banana", "orange", "strawberry"])
set->Belt.MutableSet.String.toArray /* ["apple", "banana", "orange", "strawberry"] */
```

## remove

```res sig
let remove: (t, value) => unit
```

Removes element from set. If element wasn't existed in set, value is unchanged.

```res example
let s0 = Belt.MutableSet.String.fromArray(["orange", "banana", "apple"])
s0->Belt.MutableSet.String.remove("apple")
s0->Belt.MutableSet.String.remove("banana")
s0->Belt.MutableSet.String.remove("banana")

s0->Belt.MutableSet.String.toArray /* ["orange"] */
```

## removeCheck

```res sig
let removeCheck: (t, value) => bool
```

## removeMany

```res sig
let removeMany: (t, array<value>) => unit
```

Removes each element of array from set.

```res example
let set = Belt.MutableSet.String.fromArray(["apple", "banana", "orange"])

set->Belt.MutableSet.String.removeMany(["strawberry", "apple", "banana", "orange"])
set->Belt.MutableSet.String.toArray /* [] */
```

## union

```res sig
let union: (t, t) => t
```

Returns union of two sets.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "strawberry"])
let union = Belt.MutableSet.String.union(s0, s1)
union->Belt.MutableSet.String.toArray /* ["apple", "banana", "carrot", "orange", "strawberry"] */
```

## intersect

```res sig
let intersect: (t, t) => t
```

Returns intersection of two sets.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "strawberry"])
let intersect = Belt.MutableSet.String.intersect(s0, s1)
intersect->Belt.MutableSet.String.toArray /* ["apple", "banana", "orange"] */
```

## diff

```res sig
let diff: (t, t) => t
```

Returns elements from first set, not existing in second set.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "carrot"])
let s1 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange", "strawberry"])
Belt.MutableSet.String.toArray(Belt.MutableSet.String.diff(s0, s1)) /* ["carrot"] */
Belt.MutableSet.String.toArray(Belt.MutableSet.String.diff(s1, s0)) /* ["strawberry"] */
```

## subset

```res sig
let subset: (t, t) => bool
```

Checks if second set is subset of first set.

```res example
let s0 = Belt.MutableSet.String.fromArray(["5", "2", "3", "5", "6"])
let s1 = Belt.MutableSet.String.fromArray(["5", "2", "3", "1", "5", "4"])
let s2 = Belt.MutableSet.String.intersect(s0, s1)
Belt.MutableSet.String.subset(s2, s0) /* true */
Belt.MutableSet.String.subset(s2, s1) /* true */
Belt.MutableSet.String.subset(s1, s0) /* false */
```

## cmp

```res sig
let cmp: (t, t) => int
```

Total ordering between sets. Can be used as the ordering function for doing sets of sets. It compares size first and then iterates over each element following the order of elements.

## eq

```res sig
let eq: (t, t) => bool
```

Checks if two sets are equal.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "orange"])
let s1 = Belt.MutableSet.String.fromArray(["orange", "apple"])

Belt.MutableSet.String.eq(s0, s1) /* true */
```

## forEachU

```res sig
let forEachU: (t, (. value) => unit) => unit
```

Same as [forEach](##forEach) but takes uncurried functon.

## forEach

```res sig
let forEach: (t, value => unit) => unit
```

Applies function `f` in turn to all elements of set in increasing order.

```res example
let s0 = Belt.MutableSet.String.fromArray(["banana", "orange", "apple"])
let acc = ref(list{})
s0->Belt.MutableSet.String.forEach(x => acc := Belt.List.add(acc.contents, x))
acc /* ["orange", "banana", "apple"] */
```

## reduceU

```res sig
let reduceU: (t, 'a, (. 'a, value) => 'a) => 'a
```

## reduce

```res sig
let reduce: (t, 'a, ('a, value) => 'a) => 'a
```

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "orange"])
s0->Belt.MutableSet.String.reduce(0, (acc, element) => acc + String.length(element)) /* 11 */
```

## everyU

```res sig
let everyU: (t, (. value) => bool) => bool
```

## every

```res sig
let every: (t, value => bool) => bool
```

Checks if all elements of the set satisfy the predicate. Order unspecified.

```res example
let hasAtLeastFiveChars = x => String.length(x) >= 5

let s0 = Belt.MutableSet.String.fromArray(["apple", "carrot"])
s0->Belt.MutableSet.String.every(hasAtLeastFiveChars) /* true */
```

## someU

```res sig
let someU: (t, (. value) => bool) => bool
```

## some

```res sig
let some: (t, value => bool) => bool
```

Checks if at least one element of the set satisfies the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.MutableSet.String.fromArray(["strawberry", "apple"])
s0->Belt.MutableSet.String.some(hasFiveChars) /* true */
```

## keepU

```res sig
let keepU: (t, (. value) => bool) => t
```

## keep

```res sig
let keep: (t, value => bool) => t
```

Returns the set of all elements that satisfy the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.MutableSet.String.fromArray(["apple", "orange", "banana"])
let s1 = s0->Belt.MutableSet.String.keep(hasFiveChars)

s1->Belt.MutableSet.String.toArray /* ["apple"] */
```

## partitionU

```res sig
let partitionU: (t, (. value) => bool) => (t, t)
```

## partition

```res sig
let partition: (t, value => bool) => (t, t)
```

Returns a pair of sets, where first is the set of all the elements of set that satisfy the predicate, and second is the set of all the elements of set that do not satisfy the predicate.

```res example
let hasFiveChars = x => String.length(x) == 5

let s0 = Belt.MutableSet.String.fromArray(["apple", "carrot"])
let (s1, s2) = s0->Belt.MutableSet.String.partition(hasFiveChars)

s1->Belt.MutableSet.String.toArray /* ["apple"] */
s2->Belt.MutableSet.String.toArray /* ["carrot"] */
```

## size

```res sig
let size: t => int
```

Returns size of the set.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple"])

s0->Belt.MutableSet.String.size /* 1 */
```

## toList

```res sig
let toList: t => list<value>
```

Returns list of ordered set elements.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "watermelon"])

s0->Belt.MutableSet.String.toList /* ["apple", "watermelon"] */
```

## toArray

```res sig
let toArray: t => array<value>
```

Returns array of ordered set elements.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "watermelon"])

s0->Belt.MutableSet.String.toArray /* ["apple", "watermelon"] */
```

## minimum

```res sig
let minimum: t => option<value>
```

Returns minimum value of the collection. `None` if collection is empty.

```res example
let s0 = Belt.MutableSet.String.make()
let s1 = Belt.MutableSet.String.fromArray(["apple", "orange"])

s0->Belt.MutableSet.String.minimum /* None */
s1->Belt.MutableSet.String.minimum /* Some("apple") */
```

## minUndefined

```res sig
let minUndefined: t => Js.undefined<value>
```

Returns minimum value of the collection. `undefined` if collection is empty.

```res example
let s0 = Belt.MutableSet.String.make()
let s1 = Belt.MutableSet.String.fromArray(["apple", "orange"])

s0->Belt.MutableSet.String.minUndefined /* undefined */
s1->Belt.MutableSet.String.minUndefined /* "apple" */
```

## maximum

```res sig
let maximum: t => option<value>
```

Returns maximum value of the collection. `None` if collection is empty.

```res example
let s0 = Belt.MutableSet.String.make()
let s1 = Belt.MutableSet.String.fromArray(["apple", "orange"])

s0->Belt.MutableSet.String.maximum /* None */
s1->Belt.MutableSet.String.maximum /* Some("orange") */
```

## maxUndefined

```res sig
let maxUndefined: t => Js.undefined<value>
```

Returns maximum value of the collection. `undefined` if collection is empty.

```res example
let s0 = Belt.MutableSet.String.make()
let s1 = Belt.MutableSet.String.fromArray(["apple", "orange"])

s0->Belt.MutableSet.String.maxUndefined /* undefined */
s1->Belt.MutableSet.String.maxUndefined /* orange */
```

## get

```res sig
let get: (t, value) => option<value>
```

Returns the reference of the value which is equivalent to value using the comparator specifiecd by this collection. Returns `None` if element does not exist.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "carrot"])

s0->Belt.MutableSet.String.get("carrot") /* Some("carrot") */
s0->Belt.MutableSet.String.get("watermelon") /* None */
```

## getUndefined

```res sig
let getUndefined: (t, value) => Js.undefined<value>
```

Same as [get](#get) but returns `undefined` when element does not exist.

## getExn

```res sig
let getExn: (t, value) => value
```

Same as [get](#get) but raise when element does not exist.

## split

```res sig
let split: (t, value) => ((t, t), bool)
```

Returns a tuple `((smaller, larger), present)`, `present` is true when element exist in set.

```res example
let s0 = Belt.MutableSet.String.fromArray(["apple", "banana", "orange"])

let ((smaller, larger), present) = s0->Belt.MutableSet.String.split("banana")

present /* true */
smaller->Belt.MutableSet.String.toArray /* ["apple"] */
larger->Belt.MutableSet.String.toArray /* ["orange"] */
```

## checkInvariantInternal

```res sig
let checkInvariantInternal: t => unit
```

**raise** when invariant is not held
