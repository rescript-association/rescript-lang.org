---
title: "Interop Cheatsheet"
description: "Cheatsheet for various interop scenarios in ReScript"
canonical: "/docs/manual/latest/interop-cheatsheet"
---

# Interop Cheatsheet

<!-- TODO: this should be a glossary instead -->

## Raw JS

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let add = [%raw "(a, b) => a + b"];
[%%raw "const a = 1"];
```
```ml
let add = [%raw "(a, b) => a + b"]
[%%raw "const a = 1"]
```
```js
var add = ((a, b) => a + b);
const a = 1
```

</CodeTab>

## Global Value

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.val] external setTimeout: (unit => unit, int) => float = "setTimeout";
```
```ml
external setTimeout: (unit -> unit) -> int -> float = "setTimeout" [@@bs.val]
```
```js
// Empty output
```

</CodeTab>

## Global Module's Value

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.val] [@bs.scope "Math"]
external random: unit => float = "random";

let someNumber = random();

[@bs.val] [@bs.scope ("window", "location", "ancestorOrigins")]
external length: int = "length";
```
```ml
external random: unit -> float = "random"
[@@bs.val][@@bs.scope "Math"]

let someNumber = random ()

external length: int = "length"
[@@bs.val][@@bs.scope ("window", "location", "ancestorOrigins")]
```
```js
var someNumber = Math.random();
```

</CodeTab>

## Nullable

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let a = Some(5); // compiles to 5
let b = None; // compiles to undefined
```
```ml
let a = Some 5 (* compiles to 5 *)
let b = None (* compiles to undefined *)
```
```js
var a = 5;
var b;
```

</CodeTab>

Handling a value that can be `undefined` and `null`, by ditching the `option` type and using `Js.Nullable.t`:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let jsNull = Js.Nullable.null;
let jsUndefined = Js.Nullable.undefined;
let result1: Js.Nullable.t(string) = Js.Nullable.return("hello");
let result2: Js.Nullable.t(int) = Js.Nullable.fromOption(Some(10));
let result3: option(int) = Js.Nullable.toOption(Js.Nullable.return(10));
```
```ml
let jsNull = Js.Nullable.null
let jsUndefined = Js.Nullable.undefined
let result1: string Js.Nullable.t = Js.Nullable.return "hello"
let result2: int Js.Nullable.t = Js.Nullable.fromOption (Some 10)
let result3: int option = Js.Nullable.toOption (Js.Nullable.return 10)
```
```js
var Caml_option = require("./stdlib/caml_option.js");
var Js_null_undefined = require("./stdlib/js_null_undefined.js");

var jsNull = null;
var jsUndefined;
var result1 = "hello";
var result2 = Js_null_undefined.fromOption(10);
var result3 = Caml_option.nullable_to_opt(10);
```

</CodeTab>

## JS Object

- [Bind to a JS object as a ReScript record](bind-to-js-object#bind-to-record-like-js-objects).
- [Bind to a JS object that acts like a hash map](bind-to-js-object#bind-to-hash-map-like-js-object).
- [Bind to a JS object that's a class](bind-to-js-object#bind-to-a-js-object-thats-a-class).

## Function

### Object Method & Chaining

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.send] external map: (array('a), 'a => 'b) => array('b) = "map";
[@bs.send] external filter: (array('a), 'a => 'b) => array('b) = "filter";
[|1, 2, 3|]
  ->map(a => a + 1)
  ->filter(a => a mod 2 == 0)
  ->Js.log;
```
```ml
external map: 'a array -> ('a -> 'b) -> 'b array = "map" [@@bs.send]
external filter: 'a array -> ('a -> 'b) -> 'b array = "filter" [@@bs.send]
[|1; 2; 3|]
  |. map(fun a -> a + 1)
  |. filter(fun a -> a mod 2 = 0)
  |. Js.log
```
```js
console.log(
  [1, 2, 3]
    .map(function (a) {
      return (a + 1) | 0;
    })
    .filter(function (a) {
      return a % 2 === 0;
    })
);
```

</CodeTab>

### Variadic Arguments

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.module "path"] [@bs.variadic]
external join: array(string) => string = "join";
```
```ml
external join: string array -> string = "join"
[@@bs.module "path"][@@bs.variadic]
```
```js
// Empty output
```

</CodeTab>

### Polymorphic Function

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.module "Drawing"] external drawCat: unit => unit = "draw";
[@bs.module "Drawing"] external drawDog: (~giveName: string) => unit = "draw";
```
```ml
external drawCat: unit -> unit = "draw" [@@bs.module "Drawing"]
external drawDog: giveName:string -> unit = "draw" [@@bs.module "Drawing"]
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.val]
external padLeft: (
  string,
  [@bs.unwrap] [
    | `Str(string)
    | `Int(int)
  ])
  => string = "padLeft";

padLeft("Hello World", `Int(4));
padLeft("Hello World", `Str("Message from ReScript: "));
```
```ml
external padLeft:
  string ->
  (([
    | `Str of string
    | `Int of int
  ]) [@bs.unwrap]) ->
  string = "padLeft" [@@bs.val]

let _ = padLeft "Hello World" (`Int 4)
let _ = padLeft "Hello World" (`Str "Message from ReScript: ")
```
```js
padLeft("Hello World", 4);
padLeft("Hello World", "Message from ReScript: ");
```

</CodeTab>

## JS Module Interop

[See here](import-from-export-to-js.md)

## Dangerous Type Cast

Final escape hatch converter. Do not abuse.

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
external convertToFloat: int => float = "%identity";
let age = 10;
let gpa = 2.1 +. convertToFloat(age);
```
```ml
external convertToFloat: int => float = "%identity"
let age = 10
let gpa = 2.1 +. (convertToFloat age)
```
```js
var age = 10;
var gpa = 2.1 + 10;
```

</CodeTab>

## List of `@bs` Decorators

- `@bs.as`
- `@bs.get`
- `@bs.inline`
- `@bs.int`
- `@bs.meth`
- `@bs.new`
- `@bs.return`
- `@bs.scope`
- `@bs.send`
- `@bs.set`
- `@bs.string`
- `@bs.this`
- `@bs.uncurry`
- `@bs.unwrap`
- `@bs.val`
- `@bs.variadic`
