---
title: "Variant"
description: "Variant data structures in ReScript"
canonical: "/docs/manual/latest/variant"
---

# Variant

So far, most of ReScript's data structures might look familiar to you. This section introduces an extremely important, and perhaps unfamiliar, data structure: variant.

Most data structures in most languages are about "this **and** that". A variant allows us to express "this **or** that".

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type myResponse =
  | Yes
  | No
  | PrettyMuch;

let areYouCrushingIt = Yes;
```
```ml
type myResponse =
  | Yes
  | No
  | PrettyMuch

let areYouCrushingIt = Yes
```
```js
var areYouCrushingIt = /* Yes */0;
```

</CodeTab>

`myResponse` is a variant type with the cases `Yes`, `No` and `PrettyMuch`, which are called "variant constructors" (or "variant tag"). The `|` bar separates each constructor.

**Note**: a variant's constructors need to be capitalized.

## Variant Needs an Explicit Definition

If the variant you're using is in a different file, bring it into scope like you'd do [for a record](record.md#record-needs-an-explicit-definition):

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// Zoo.re
type animal = Dog | Cat | Bird;
```
```ml
(* Zoo.re *)
type animal = Dog | Cat | Bird
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// Example.re
let pet: Zoo.animal = Dog; // preferred
// or
let pet2 = Zoo.Dog;
```
```ml
(* Example.re *)
let pet: Zoo.animal = Dog (* preferred *)
(* or *)
let pet2 = Zoo.Dog
```
```js
var pet = /* Dog */0;
var pet2 = /* Dog */0;
```

</CodeTab>

## Constructor Arguments

A variant's constructors can hold extra data separated by comma.

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type account =
  | None
  | Instagram(string)
  | Facebook(string, int);
```
```ml
type account =
  | None
  | Instagram of string
  | Facebook of string * int
```
```js
// Empty output
```

</CodeTab>

Here, `Instagram` holds a `string`, and `Facebook` holds a `string` and an `int`. Usage:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
let myAccount = Facebook("Josh", 26);
let friendAccount = Instagram("Jenny");
```
```ml
let myAccount =
  Facebook ("Josh", 26)
let friendAccount =
  Instagram "Jenny"
```
```js
var myAccount = {
  TAG: /* Facebook */1,
  _0: "Josh",
  _1: 26
};
var friendAccount = {
  TAG: /* Instagram */0,
  _0: "Jenny"
};
```

</CodeTab>

### Records in Variants

You can use a record type in a variant:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type idType = {name: string, password: string};

type user =
  | Number(int)
  | Id(idType);
```
```ml
type idType = {name: string; password: string}

type user =
  | Number of int
  | Id of idType
```
```js
// Empty output
```

</CodeTab>

If the record type is used only in the variant definition, you may put it in line:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type user =
  | Number(int)
  | Id({name: string, password: string});
```
```ml
type user =
  | Number of int
  | Id of {name: string; password: string}
```
```js
// Empty output
```

</CodeTab>

### Pattern Matching On Variant

See the [Pattern Matching/Destructuring](pattern-matching-destructuring.md) section later.

## Tips & Tricks

**Be careful** not to confuse a constructor carrying 2 arguments with a constructor carrying a single tuple argument:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type account =
  | Facebook(string, int); // 2 arguments
type account2 =
  | Instagram((string, int)); // 1 argument - happens to be a 2-tuple
```
```ml
type account =
  | Facebook of string * int (* 2 arguments *)
type account2 =
  | Instagram of (string * int) (* 1 argument - happens to be a 2-tuple *)
```
```js
// Empty output
```

### Variants Must Have Constructors

</CodeTab>

If you come from an untyped language, you might be tempted to try `type myType = int | string`. This isn't possible in ReScript; you'd have to give each branch a constructor: `type myType = Int(int) | String(string)`. The former looks nice, but causes lots of trouble down the line.

### Interop with JavaScript

_This section assumes knowledge about our JavaScript interop. Skip this if you haven't felt the itch to use variants for wrapping JS functions yet_.

Quite a few JS libraries use functions that can accept many types of arguments. In these cases, it's very tempting to model them as variants. For example, suppose there's a `myLibrary.draw` JS function that takes in either a `number` or a `string`. You might be tempted to bind it like so:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
// reserved for internal usage
[@bs.module "myLibrary"] external draw: 'a => unit = "draw";

type animal =
  | MyFloat(float)
  | MyString(string);

let betterDraw = (animal) =>
  switch (animal) {
  | MyFloat(f) => draw(f)
  | MyString(s) => draw(s)
  };

betterDraw(MyFloat(1.5));
```
```ml
(* reserved for internal usage *)
external draw: 'a => unit = "draw" [@@bs.module "myLibrary"]

type animal =
  | MyFloat of float
  | MyString of string

let betterDraw animal =
  match animal with
  | MyFloat f -> draw f
  | MyString s -> draw s

let () = betterDraw (MyFloat 1.5)
```
```js
var MyLibrary = require("myLibrary");

function betterDraw(animal) {
  MyLibrary.draw(animal._0);
}

betterDraw({
  TAG: /* MyFloat */0,
  _0: 1.5
});
```

</CodeTab>

You could definitely do that, but there are better ways! For example, define two `external`s that both compile to the same JS call:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
[@bs.module "myLibrary"] external drawFloat: float => unit = "draw";
[@bs.module "myLibrary"] external drawString: string => unit = "draw";
```
```ml
external drawFloat: float -> unit = "draw" [@@bs.module "myLibrary"]
external drawString: string -> unit = "draw" [@@bs.module "myLibrary"]
```
```js
// Empty output
```

</CodeTab>

ReScript also provides [a few other ways](bind-to-js-function.md#modeling-polymorphic-function) to do this.

### Variant Types Are Found By Field Name

Please refer to this [record section](record#record-types-are-found-by-field-name). Variants are the same: a function can't accept an arbitrary constructor shared by two different variants. Again, such feature exists; it's called a polymorphic variant. We'll talk about this in the future =).

## Design Decisions

Variants, in their many forms (polymorphic variant, open variant, GADT, etc.), are likely _the_ feature of a type system such as ReScript's. The aforementioned `option` variant, for example, obliterates the need for nullable types, a major source of bugs in other languages. Philosophically speaking, a problem is composed of many possible branches/conditions. Mishandling these conditions is the majority of what we call bugs. **A type system doesn't magically eliminate bugs; it points out the unhandled conditions and asks you to cover them**\*. The ability to model "this or that" correctly is crucial.

For example, some folks wonder how the type system can safely eliminate badly formatted JSON data from propagating into their program. They don't, not by themselves! But if the parser returns the `option` type `None | Some(actualData)`, then you'd have to handle the `None` case explicitly in later call sites. That's all there is.

Performance-wise, a variant can potentially tremendously speed up your program's logic. Here's a piece of JavaScript:

```js
let data = 'dog'
if (data === 'dog') {
  ...
} else if (data === 'cat') {
  ...
} else if (data === 'bird') {
  ...
}
```

There's a linear amount of branch checking here (`O(n)`). Compare this to using a ReScript variant:

<CodeTab labels={["Reason (Old Syntax)", "ML (Older Syntax)", "JS Output"]}>

```re
type animal = Dog | Cat | Bird;
let data = Dog
switch (data) {
| Dog => Js.log("Wof")
| Cat => Js.log("Meow")
| Bird => Js.log("Kashiiin")
}
```
```ml
type animal = Dog | Cat | Bird
let data = Dog
let () =
  match data with
  | Dog -> Js.log "Wof"
  | Cat -> Js.log "Meow"
  | Bird -> Js.log "Kashiiin"
```
```js
console.log("Wof");

var data = /* Dog */0;
```

</CodeTab>

The compiler sees the variant, then

1. conceptually turns them into `type animal = 0 | 1 | 2`
2. compiles `switch` to a constant-time jump table (`O(1)`).
