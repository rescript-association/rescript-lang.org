# MutableSetString

<Intro>

This module is [Belt.MutableSet](./mutable-set) specialized with key type to be a `string` type.
It is more efficient in general, the API is the same with [Belt.MutableSet](./mutable-set) except its key type is fixed, and identity is not needed (using the built-in one)

</Intro>

## value

```re sig
type value = string;
```

The type of the set elements.

## t

```re sig
type t;
```

The type of sets.

## make

```re sig
let make: unit => t;
```

Returns empty set.

```re example
let set = Belt.MutableSet.String.make();
```

## fromArray

```re sig
let fromArray: array(value) => t;
```

Creates new set from array of elements.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "orange", "banana"|])

s0->Belt.MutableSet.String.toArray; /* [|"apple", "banana", "orange"|] */
```

## fromSortedArrayUnsafe

```re sig
let fromSortedArrayUnsafe: array(value) => t;
```

The same as [fromArray][#fromarray] except it is after assuming the input array is already sorted.

## copy

```re sig
let copy: t => t;
```

Returns copy of a set.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"orange", "apple"|])

let copied = s0->Belt.MutableSet.String.copy;
copied->Belt.MutableSet.String.toArray /* [|"apple", "orange"|] */
```

## isEmpty

```re sig
let isEmpty: t => bool;
```

Checks if set is empty.

```re example
let empty = Belt.MutableSet.String.fromArray([||]);
let notEmpty = Belt.MutableSet.String.fromArray([|"apple"|]);

Belt.MutableSet.String.isEmpty(empty); /* true */
Belt.MutableSet.String.isEmpty(notEmpty); /* false */
```

## has

```re sig
let has: (t, value) => bool;
```

Checks if element exists in set.

```re example
let set = Belt.MutableSet.String.fromArray([|"apple", "orange", "banana"|]);

set->Belt.MutableSet.String.has("strawberry") /* false */
set->Belt.MutableSet.String.has("apple") /* true */
```

## add

```re sig
let add: (t, value) => unit;
```

Adds element to set. If element existed in set, value is unchanged.

```re example
let s0 = Belt.MutableSet.String.make();
s0->Belt.MutableSet.String.add("apple");
s0->Belt.MutableSet.String.add("banana");
s0->Belt.MutableSet.String.add("banana");

s0->Belt.MutableSet.String.toArray; /* [|"apple", "banana"|] */
```

## addCheck

```re sig
let addCheck: (t, value) => bool;
```

## mergeMany

```re sig
let mergeMany: (t, array(value)) => unit;
```

Adds each element of array to set.

```re example
let set = Belt.MutableSet.String.make();

set->Belt.MutableSet.String.mergeMany([|"apple", "banana", "orange", "strawberry"|]);
set->Belt.MutableSet.String.toArray; /* [|"apple", "banana", "orange", "strawberry"|] */
```

## remove

```re sig
let remove: (t, value) => unit;
```

Removes element from set. If element wasn't existed in set, value is unchanged.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"orange", "banana", "apple"|]);
s0->Belt.MutableSet.String.remove("apple");
s0->Belt.MutableSet.String.remove("banana");
s0->Belt.MutableSet.String.remove("banana");

s0->Belt.MutableSet.String.toArray; /* [|"orange"|] */
```

## removeCheck

```re sig
let removeCheck: (t, value) => bool;
```

## removeMany

```re sig
let removeMany: (t, array(value)) => unit;
```

Removes each element of array from set.

```re example
let set = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange"|]);

set->Belt.MutableSet.String.removeMany([|"strawberry", "apple", "banana", "orange"|]);
set->Belt.MutableSet.String.toArray; /* [||] */
```

## union

```re sig
let union: (t, t) => t;
```

Returns union of two sets.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "carrot"|]);
let s1 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "strawberry"|]);
let union = Belt.MutableSet.String.union(s0, s1);
union->Belt.MutableSet.String.toArray; /* [|"apple", "banana", "carrot", "orange", "strawberry"|] */
```

## intersect

```re sig
let intersect: (t, t) => t;
```

Returns intersection of two sets.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "carrot"|]);
let s1 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "strawberry"|]);
let intersect = Belt.MutableSet.String.intersect(s0, s1);
intersect->Belt.MutableSet.String.toArray; /* [|"apple", "banana", "orange"|] */
```

## diff

```re sig
let diff: (t, t) => t;
```

Returns elements from first set, not existing in second set.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "carrot"|]);
let s1 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange", "strawberry"|]);
Belt.MutableSet.String.toArray(Belt.MutableSet.String.diff(s0, s1)); /* [|"carrot"|] */
Belt.MutableSet.String.toArray(Belt.MutableSet.String.diff(s1, s0)); /* [|"strawberry"|] */
```

## subset

```re sig
let subset: (t, t) => bool;
```

Checks if second set is subset of first set.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"5", "2", "3", "5", "6"|]);
let s1 = Belt.MutableSet.String.fromArray([|"5", "2", "3", "1", "5", "4"|]);
let s2 = Belt.MutableSet.String.intersect(s0, s1);
Belt.MutableSet.String.subset(s2, s0); /* true */
Belt.MutableSet.String.subset(s2, s1); /* true */
Belt.MutableSet.String.subset(s1, s0); /* false */
```

## cmp

```re sig
let cmp: (t, t) => int;
```

Total ordering between sets. Can be used as the ordering function for doing sets of sets. It compares size first and then iterates over each element following the order of elements.

## eq

```re sig
let eq: (t, t) => bool;
```

Checks if two sets are equal.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);
let s1 = Belt.MutableSet.String.fromArray([|"orange", "apple"|]);

Belt.MutableSet.String.eq(s0, s1); /* true */
```

## forEachU

```re sig
let forEachU: (t, [@bs] (value => unit)) => unit;
```

Same as [forEach](##forEach) but takes uncurried functon.

## forEach

```re sig
let forEach: (t, value => unit) => unit;
```

Applies function `f` in turn to all elements of set in increasing order.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"banana", "orange", "apple"|]);
let acc = ref([]);
s0->Belt.MutableSet.String.forEach(x => {
  acc := Belt.List.add(acc^, x)
});
acc; /* ["orange", "banana", "apple"] */
```

## reduceU

```re sig
let reduceU: (t, 'a, [@bs] (('a, value) => 'a)) => 'a;
```

## reduce

```re sig
let reduce: (t, 'a, ('a, value) => 'a) => 'a;
```

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);
s0->Belt.MutableSet.String.reduce(0, (acc, element) =>
  acc + String.length(element)
); /* 11 */
```

## everyU

```re sig
let everyU: (t, [@bs] (value => bool)) => bool;
```

## every

```re sig
let every: (t, value => bool) => bool;
```

Checks if all elements of the set satisfy the predicate. Order unspecified.

```re example
let hasAtLeastFiveChars = x => String.length(x) >= 5;

let s0 = Belt.MutableSet.String.fromArray([|"apple", "carrot"|]);
s0->Belt.MutableSet.String.every(hasAtLeastFiveChars); /* true */
```

## someU

```re sig
let someU: (t, [@bs] (value => bool)) => bool;
```

## some

```re sig
let some: (t, value => bool) => bool;
```

Checks if at least one element of the set satisfies the predicate.

```re example
let hasFiveChars = x => String.length(x) == 5;

let s0 = Belt.MutableSet.String.fromArray([|"strawberry", "apple"|]);
s0->Belt.MutableSet.String.some(hasFiveChars); /* true */
```

## keepU

```re sig
let keepU: (t, [@bs] (value => bool)) => t;
```

## keep

```re sig
let keep: (t, value => bool) => t;
```

Returns the set of all elements that satisfy the predicate.

```re example
let hasFiveChars = x => String.length(x) == 5;

let s0 = Belt.MutableSet.String.fromArray([|"apple", "orange", "banana"|]);
let s1 = s0->Belt.MutableSet.String.keep(hasFiveChars);

s1->Belt.MutableSet.String.toArray; /* [|"apple"|] */
```

## partitionU

```re sig
let partitionU: (t, [@bs] (value => bool)) => (t, t);
```

## partition

```re sig
let partition: (t, value => bool) => (t, t);
```

Returns a pair of sets, where first is the set of all the elements of set that satisfy the predicate, and second is the set of all the elements of set that do not satisfy the predicate.

```re example
let hasFiveChars = x => String.length(x) == 5;

let s0 = Belt.MutableSet.String.fromArray([|"apple", "carrot"|]);
let (s1, s2) = s0->Belt.MutableSet.String.partition(hasFiveChars);

s1->Belt.MutableSet.String.toArray; /* [|"apple"|] */
s2->Belt.MutableSet.String.toArray; /* [|"carrot"|] */
```

## size

```re sig
let size: t => int;
```

Returns size of the set.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple"|]);

s0->Belt.MutableSet.String.size; /* 1 */
```

## toList

```re sig
let toList: t => list(value);
```

Returns list of ordered set elements.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "watermelon"|]);

s0->Belt.MutableSet.String.toList; /* ["apple", "watermelon"] */
```

## toArray

```re sig
let toArray: t => array(value);
```

Returns array of ordered set elements.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "watermelon"|]);

s0->Belt.MutableSet.String.toArray; /* [|"apple", "watermelon"|] */
```

## minimum

```re sig
let minimum: t => option(value);
```

Returns minimum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.MutableSet.String.make();
let s1 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);

s0->Belt.MutableSet.String.minimum; /* None */
s1->Belt.MutableSet.String.minimum; /* Some("apple") */
```

## minUndefined

```re sig
let minUndefined: t => Js.undefined(value);
```

Returns minimum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.MutableSet.String.make();
let s1 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);

s0->Belt.MutableSet.String.minUndefined; /* undefined */
s1->Belt.MutableSet.String.minUndefined; /* "apple" */
```

## maximum

```re sig
let maximum: t => option(value);
```

Returns maximum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.MutableSet.String.make();
let s1 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);

s0->Belt.MutableSet.String.maximum; /* None */
s1->Belt.MutableSet.String.maximum; /* Some("orange") */
```

## maxUndefined

```re sig
let maxUndefined: t => Js.undefined(value);
```

Returns maximum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.MutableSet.String.make();
let s1 = Belt.MutableSet.String.fromArray([|"apple", "orange"|]);

s0->Belt.MutableSet.String.maxUndefined; /* undefined */
s1->Belt.MutableSet.String.maxUndefined; /* orange */
```

## get

```re sig
let get: (t, value) => option(value);
```

Returns the reference of the value which is equivalent to value using the comparator specifiecd by this collection. Returns `None` if element does not exist.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "carrot"|]);

s0->Belt.MutableSet.String.get("carrot"); /* Some("carrot") */
s0->Belt.MutableSet.String.get("watermelon"); /* None */
```

## getUndefined

```re sig
let getUndefined: (t, value) => Js.undefined(value);
```

Same as [get](#get) but returns `undefined` when element does not exist.

## getExn

```re sig
let getExn: (t, value) => value;
```

Same as [get](#get) but raise when element does not exist.

## split

```re sig
let split: (t, value) => ((t, t), bool);
```

Returns a tuple `((smaller, larger), present)`, `present` is true when element exist in set.

```re example
let s0 = Belt.MutableSet.String.fromArray([|"apple", "banana", "orange"|]);

let ((smaller, larger), present) = s0->Belt.MutableSet.String.split("banana");

present; /* true */
smaller->Belt.MutableSet.String.toArray; /* [|"apple"|] */
larger->Belt.MutableSet.String.toArray; /* [|"orange"|] */
```

## checkInvariantInternal

```re sig
let checkInvariantInternal: t => unit;
```

**raise** when invariant is not held
