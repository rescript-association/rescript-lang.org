# MutableSetInt

<Intro>

This module is [Belt.MutableSet](./mutable-set) specialized with key type to be a `int` type.
It is more efficient in general, the API is the same with [Belt.MutableSet](./mutable-set) except its key type is fixed, and identity is not needed (using the built-in one).

</Intro>

## value

```re sig
type value = int;
```

The type of the set elements

## t

```re sig
type t;
```

Type of the sets.

## make

```re sig
let make: unit => t;
```

Returns empty set.

```re example
let set = Belt.MutableSet.Int.make();
```

## fromArray

```re sig
let fromArray: array(value) => t;
```

Creates new set from array of elements.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|1, 3, 2, 4|])

s0->Belt.MutableSet.Int.toArray; /* [|1, 2, 3, 4|] */
```

## fromSortedArrayUnsafe

```re sig
let fromSortedArrayUnsafe: array(value) => t;
```

The same as [fromArray][#fromarray] except it is after assuming the input array is already sorted.

## copy

```re sig
let copy: t => t;
```

Returns copy of a set.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|1, 3, 2, 4|])

let copied = s0->Belt.MutableSet.Int.copy;
copied->Belt.MutableSet.Int.toArray /* [|1, 2, 3, 4|] */
```

## isEmpty

```re sig
let isEmpty: t => bool;
```

Checks if set is empty.

```re example
let empty = Belt.MutableSet.Int.fromArray([||]);
let notEmpty = Belt.MutableSet.Int.fromArray([|1|]);

Belt.MutableSet.Int.isEmpty(empty); /* true */
Belt.MutableSet.Int.isEmpty(notEmpty); /* false */
```

## has

```re sig
let has: (t, value) => bool;
```

Checks if element exists in set.

```re example
let set = Belt.MutableSet.Int.fromArray([|1, 4, 2, 5|]);

set->Belt.MutableSet.Int.has(3) /* false */
set->Belt.MutableSet.Int.has(1) /* true */
```

## add

```re sig
let add: (t, value) => unit;
```

Adds element to set. If element existed in set, value is unchanged.

```re example
let s0 = Belt.MutableSet.Int.make();
s0->Belt.MutableSet.Int.add(1);
s0->Belt.MutableSet.Int.add(2);
s0->Belt.MutableSet.Int.add(2);

s0->Belt.MutableSet.Int.toArray; /* [|1, 2|] */
```

## addCheck

```
let addCheck: (t, value) => bool;
```

## mergeMany

```re sig
let mergeMany: (t, array(value)) => unit;
```

Adds each element of array to set. Unlike [add](#add), the reference of return value might be changed even if all values in array already exist in set

```re example
let set = Belt.MutableSet.Int.make();

set->Belt.MutableSet.Int.mergeMany([|5, 4, 3, 2, 1|]);
set->Belt.MutableSet.Int.toArray; /* [|1, 2, 3, 4, 5|] */
```

## remove

```re sig
let remove: (t, value) => unit;
```

Removes element from set. If element wasn't existed in set, value is unchanged.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|2,3,1,4,5|]);
s0->Belt.MutableSet.Int.remove(1);
s0->Belt.MutableSet.Int.remove(3);
s0->Belt.MutableSet.Int.remove(3);

s0->Belt.MutableSet.Int.toArray; /* [|2,4,5|] */
```

## removeCheck

```re sig
let removeCheck: (t, value) => bool;
```

## removeMany

```re sig
let removeMany: (t, array(value)) => unit;
```

Removes each element of array from set.

```re example
let set = Belt.MutableSet.Int.fromArray([|1, 2, 3, 4|]);

set->Belt.MutableSet.Int.removeMany([|5, 4, 3, 2, 1|]);
set->Belt.MutableSet.Int.toArray; /* [||] */
```

## union

```re sig
let union: (t, t) => t;
```

Returns union of two sets.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.MutableSet.Int.fromArray([|5,2,3,1,5,4|]);
let union = Belt.MutableSet.Int.union(s0, s1);
union->Belt.MutableSet.Int.toArray; /* [|1,2,3,4,5,6|] */
```

## intersect

```re sig
let intersect: (t, t) => t;
```

Returns intersection of two sets.

```re example

let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.MutableSet.Int.fromArray([|5,2,3,1,5,4|]);
let intersect = Belt.MutableSet.Int.intersect(s0, s1);
intersect->Belt.MutableSet.Int.toArray; /* [|2,3,5|] */
```

## diff

```re sig
let diff: (t, t) => t;
```

Returns elements from first set, not existing in second set.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.MutableSet.Int.fromArray([|5,2,3,1,5,4|]);
Belt.MutableSet.Int.toArray(Belt.MutableSet.Int.diff(s0, s1)); /* [|6|] */
Belt.MutableSet.Int.toArray(Belt.MutableSet.Int.diff(s1,s0)); /* [|1,4|] */
```

## subset

```re sig
let subset: (t, t) => bool;
```

Checks if second set is subset of first set.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.MutableSet.Int.fromArray([|5,2,3,1,5,4|]);
let s2 = Belt.MutableSet.Int.intersect(s0, s1);
Belt.MutableSet.Int.subset(s2, s0); /* true */
Belt.MutableSet.Int.subset(s2, s1); /* true */
Belt.MutableSet.Int.subset(s1, s0); /* false */
```

## cmp

```re sig
let cmp: (t, t) => int;
```

Total ordering between sets. Can be used as the ordering function for doing sets of sets. It compares size first and then iterates over each element following the order of elements.

## eq

```re sig
let eq: (t, t) => bool;
```

Checks if two sets are equal.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3|]);
let s1 = Belt.MutableSet.Int.fromArray([|3,2,5|]);

Belt.MutableSet.Int.eq(s0, s1); /* true */
```

## forEachU

```re sig
let forEachU: (t, [@bs] (value => unit)) => unit;
```

Same as [forEach](##forEach) but takes uncurried functon.

## forEach

```re sig
let forEach: (t, value => unit) => unit;
```

Applies function `f` in turn to all elements of set in increasing order.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
let acc = ref([]);
s0->Belt.MutableSet.Int.forEach(x => {
  acc := Belt.List.add(acc^, x)
});
acc; /* [6,5,3,2] */
```

## reduceU

```re sig
let reduceU: (t, 'a, [@bs] (('a, value) => 'a)) => 'a;
```

## reduce

```
let reduce: (t, 'a, ('a, value) => 'a) => 'a;
```

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|5,2,3,5,6|]);
s0->Belt.MutableSet.Int.reduce([], (acc, element) =>
  acc->Belt.List.add(element)
); /* [6,5,3,2] */
```

## everyU

```re sig
let everyU: (t, [@bs] (value => bool)) => bool;
```

## every

```re sig
let every: (t, value => bool) => bool;
```

Checks if all elements of the set satisfy the predicate. Order unspecified.

```re example
let isEven = x => x mod 2 == 0;

let s0 = Belt.MutableSet.Int.fromArray([|2,4,6,8|]);
s0->Belt.MutableSet.Int.every(isEven); /* true */
```

## someU

```re sig
let someU: (t, [@bs] (value => bool)) => bool;
```

## some

```re sig
let some: (t, value => bool) => bool;
```

Checks if at least one element of the set satisfies the predicate.

```re example
let isOdd = x => x mod 2 != 0;

let s0 = Belt.MutableSet.Int.fromArray([|1,2,4,6,8|]);
s0->Belt.MutableSet.Int.some(isOdd); /* true */
```

## keepU

```re sig
let keepU: (t, [@bs] (value => bool)) => t;
```

## keep

```re sig
let keep: (t, value => bool) => t;
```

Returns the set of all elements that satisfy the predicate.

```re example
let isEven = x => x mod 2 == 0;

let s0 = Belt.MutableSet.Int.fromArray([|1,2,3,4,5|]);
let s1 = s0->Belt.MutableSet.Int.keep(isEven);

s1->Belt.MutableSet.Int.toArray; /* [|2, 4|] */
```

## partitionU

```re sig
let partitionU: (t, [@bs] (value => bool)) => (t, t);
```

## partition

```re sig
let partition: (t, value => bool) => (t, t);
```

```re example
let isOdd = x => x mod 2 != 0;

let s0 = Belt.MutableSet.Int.fromArray([|1,2,3,4,5|]);
let (s1, s2) = s0->Belt.MutableSet.Int.partition(isOdd);

s1->Belt.MutableSet.Int.toArray; /* [|1,3,5|] */
s2->Belt.MutableSet.Int.toArray; /* [|2,4|] */
```

## size

```re sig
let size: t => int;
```

Returns size of the set.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|1,2,3,4|]);

s0->Belt.MutableSet.Int.size; /* 4 */
```

## toList

```re sig
let toList: t => list(value);
```

Returns list of ordered set elements.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.toList; /* [1,2,3,5] */
```

## toArray

```re sig
let toArray: t => array(value);
```

Returns array of ordered set elements.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.toArray; /* [|1,2,3,5|] */
```

## minimum

```re sig
let minimum: t => option(value);
```

Returns minimum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.MutableSet.Int.make();
let s1 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.minimum; /* None */
s1->Belt.MutableSet.Int.minimum; /* Some(1) */
```

## minUndefined

```re sig
let minUndefined: t => Js.undefined(value);
```

Returns minimum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.MutableSet.Int.make();
let s1 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.minUndefined; /* undefined */
s1->Belt.MutableSet.Int.minUndefined; /* 1 */
```

## maximum

```re sig
let maximum: t => option(value);
```

Returns maximum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.MutableSet.Int.make();
let s1 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.maximum; /* None */
s1->Belt.MutableSet.Int.maximum; /* Some(5) */
```

## maxUndefined

```re sig
let maxUndefined: t => Js.undefined(value);
```

Returns maximum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.MutableSet.Int.make();
let s1 = Belt.MutableSet.Int.fromArray([|3,2,1,5|]);

s0->Belt.MutableSet.Int.maxUndefined; /* undefined */
s1->Belt.MutableSet.Int.maxUndefined; /* 5 */
```

## get

```re sig
let get: (t, value) => option(value);
```

Returns the reference of the value which is equivalent to value using the comparator specifiecd by this collection. Returns `None` if element does not exist.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|1,2,3,4,5|]);

s0->Belt.MutableSet.Int.get(3); /* Some(3) */
s0->Belt.MutableSet.Int.get(20); /* None */
```

## getUndefined

```re sig
let getUndefined: (t, value) => Js.undefined(value);
```

Same as [get](#get) but returns `undefined` when element does not exist.

## getExn

```re sig
let getExn: (t, value) => value;
```

Same as [get](#get) but raise when element does not exist.

## split

```re sig
let split: (t, value) => ((t, t), bool);
```

Returns a tuple `((smaller, larger), present)`, `present` is true when element exist in set.

```re example
let s0 = Belt.MutableSet.Int.fromArray([|1,2,3,4,5|]);

let ((smaller, larger), present) = s0->Belt.MutableSet.Int.split(3);

present; /* true */
smaller->Belt.MutableSet.Int.toArray; /* [|1,2|] */
larger->Belt.MutableSet.Int.toArray; /* [|4,5|] */

```

## checkInvariantInternal

```re sig
let checkInvariantInternal: t => unit;
```

**raise** when invariant is not held
